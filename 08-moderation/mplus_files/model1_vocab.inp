TITLE:
m1 model moderation
DATA:
FILE = "/Users/agarber/github/Quant-Fish-END/08-moderation/mplus_files/model1_vocab.dat";
 
VARIABLE:
NAMES = year sex education vocabulary; 
 MISSING=.;
 usevar =
    year              !!! covariate/moderator
    education         !!! covariate
    vocabulary        !!! outcome
    int_yred;         !!! interaction of year and education
DEFINE:
center education (grandmean);
     int_yred = year*education;  !!! create interaction term !!!
ANALYSIS:
estimator = MLR
MODEL:
[vocabulary](b0); !!! intercept !!!
   
    vocabulary on     !!! outcome !!!
    year(b1)          !!! covariate/moderator !!!
    education(b2)     !!! covariate !!!
    int_yred(b3);     !!! interaction of year and education !!! 
MODEL CONSTRAINT:
LOOP(x,-1,1,0.01); 
   PLOT(y1974 y2016);
   y1974 = b0 + b2*x;
   y2016 = b0 + b1 + (b2 + b3)*x;
   
   new(hi_y1974 lo_y1974 hi_y2016 lo_y2016 diff_hi); 
   
   hi_y1974 = b0 + b2*(6.28);  !!! value (6.28) used is plus-minus 2 SD's on X !!!
   lo_y1974 = b0 + b2*(-6.28);
   hi_y2016 = b0 + b1 + (b2 + b3)*(6.28);
   lo_y2016 = b0 + b1 + (b2 + b3)*(-6.28);
   
   diff_hi = hi_y2016 - hi_y1974; !!! test the significance of distance between 2 points !!! 
OUTPUT:
sampstat standardized modindices (3.84)
PLOT:
type=plot3;
